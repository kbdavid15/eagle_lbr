#include "json.inc"

string apiKey = "0c9d5f73";
char gs = 29;

string chrrep(string str,char a,char b){
	for(int i = 0;i<strlen(str);i++){
		if(str[i]==a){
			str[i] = b;
		}
	}
	return(str);
}

string octopartSearch(string mpn) {
	string match_url = "http://octopart.com/api/v3/parts/match?";
	string query = "queries=[{\"mpn\": \"" + mpn + "\"}]";
	query += "&apikey=" + apiKey;
	query += "&include[]=specs";

	string Result;
	if (netget(Result, match_url + query) >= 0) {
		// process results
		return(Result);
	}
	else {
		dlgMessageBox(neterror());
	}
	return "";
}

string process_octopart_result(string json_text) {
	string keylist = json_GetKeyList(json_text);
	if (strstr(keylist, "results") < 0) {
		dlgMessageBox("Incorrect result format:\n" + keylist);
		return "Incorrect result format:\n" + keylist;
	}
	string results = json_GetKeyItem(json_text, "results");
	string first_result = json_GetArrayItem(results, 0);	// get the first result items
	string items = json_GetKeyItem(first_result, "items");
	string first_item = json_GetArrayItem(items, 0);	//TODO: Show multiple items for selection
	string specs = json_GetKeyItem(first_item, "specs");
	string mpn = json_GetKeyItem(first_item, "mpn");
	string manuf_obj = json_GetKeyItem(first_item, "manufacturer");
	string manuf = json_GetKeyItem(manuf_obj, "name");

	string ret_val = "manufacturer\t" + manuf + gs;
	ret_val += "mpn\t" + mpn + gs;

	// process specs
	string speclist[];
	strsplit(speclist, json_GetKeyList(specs), '\t');

	for (int i = 0; speclist[i]; i++) {
		string spec_obj = json_GetKeyItem(specs, speclist[i]);
		string display_val = json_GetKeyItem(spec_obj, "display_value");
		ret_val += speclist[i] + "\t" + display_val + gs;
	}


	return ret_val;
}



void main(void){
	string json_data;
	int idx;
	int array_idx;
	int idx2;
	string strResult;
	string key;
	string mpn_search;
	string attributes[];
	string listHeader = "Name\tValue";

	dlgDialog("JSON Tester") {
		dlgVBoxLayout{
			dlgHBoxLayout {
				dlgLabel("Enter part to search Octopart");
				dlgStringEdit(mpn_search);
				dlgPushButton("Search") {
					json_data = octopartSearch(mpn_search);
					string results = process_octopart_result(json_data);
					strsplit(attributes, results, gs);
				}
			}
			dlgHBoxLayout{
				dlgVBoxLayout{
					dlgLabel("json_data=");
					dlgTextView(json_data);
				}
				dlgVBoxLayout{

					dlgPushButton("json_GetKeyList(json_data)"){
						strResult = chrrep(json_GetKeyList(json_data),'\t','\n');
					};

					dlgPushButton("json_GetArrayCount(json_data)"){
						sprintf(strResult,"%d",json_GetArrayCount(json_data));
					};

					dlgStretch(1);
					dlgLabel("key=");
					dlgStringEdit(key);
					dlgPushButton("json_GetKeyItem(json_data,key)"){
						strResult = json_GetKeyItem(json_data,key);
					};

					dlgStretch(1);
					dlgLabel("array_idx=");
					dlgIntEdit(array_idx);
					dlgPushButton("json_GetArrayItem(json_data,array_idx)"){
						strResult = json_GetArrayItem(json_data,array_idx);
					};

					dlgStretch(1);
					dlgPushButton("<-- Copy ---"){
						json_data = strResult;
						strResult = "";
					};
					dlgStretch(1);
					dlgPushButton("Reset"){
						fileread(json_data,filedir(argv[0]) + "data.txt");
						strResult = "";
					};
				}
				dlgVBoxLayout{
					dlgLabel("Result=");
					dlgTextView(strResult);

				}
				dlgVBoxLayout{
					dlgLabel("Result=");
					string Colors[] = { "red\tThe color RED", "green\tThe color GREEN", "blue\tThe color BLUE" };
					int Selected = 0; // initially selects "red"
					dlgListView(listHeader, attributes, Selected) dlgMessageBox("You have selected " + attributes[Selected]);
				}
			}
		}
	};
}
